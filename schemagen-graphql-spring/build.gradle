ext {
    springVersion = '4.2.2.RELEASE'
}
dependencies {
    compile(project(':')) {
        transitive = true
    }
    compile "org.springframework:spring-core:${springVersion}"
    compile "org.springframework:spring-context:${springVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "org.mockito:mockito-all:${mockitoVersion}"
}


publishing {
    publications {
        schemagenGraphQLSpring(MavenPublication) {
            version version
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    description project.description

                    url "https://github.com/bpatters/schemagen-graphql"
                    scm {
                        url "https://github.com/bpatters/schemagen-graphql"
                        connection "https://github.com/bpatters/schemagen-graphql"
                        developerConnection "https://github.com/bpatters/schemagen-graphql"
                    }
                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/bpatters/schemagen-graphql/blob/master/LICENSE.md'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'bpatters'
                            name 'Bret Patterson'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    publications = ['schemagenGraphQLSpring']
    publish = true
    pkg {
        repo = 'schemagen-graphql'
        name = 'schemagen-graphql-spring'
        desc = 'Spring Datafetchers for GraphQL'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/bpatters/schemagen-graphql'
    }
}
