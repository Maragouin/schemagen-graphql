plugins {
    id "com.jfrog.bintray" version "1.4"
}

def releaseVersion = System.properties.RELEASE_VERSION

allprojects {
    description "This provides type introspection and generation of GraphQL from Pojos."
    group 'com.bretpatterson'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    ext {
        jacksonVersion = "2.5.1";
    }
    version = releaseVersion ? releaseVersion : "SNAPSHOT"

    jar {
        from "${project.rootDir}/LICENSE.md"
    }

    task sourcesJar(type: Jar) {
        dependsOn classes
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives file("build/libs/${project.name}-" + project.version + ".jar")
        archives sourcesJar
        archives javadocJar
    }

    repositories {
        mavenLocal();
        mavenCentral();
    }

    publishing {
        publications {
            schemagenGraphQL(MavenPublication) {
                version version
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = Closure.DELEGATE_FIRST
                        name project.name
                        description project.description

                        url "https://github.com/bpatters/schemagen-graphql"
                        scm {
                            url "https://github.com/bpatters/schemagen-graphql"
                            connection "https://github.com/bpatters/schemagen-graphql"
                            developerConnection "https://github.com/bpatters/schemagen-graphql"
                        }
                        licenses {
                            license {
                                name 'MIT'
                                url 'https://github.com/bpatters/schemagen-graphql/blob/master/LICENSE.md'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'bpatters'
                                name 'Bret Patterson'
                            }
                        }
                    }
                }
            }
        }
    }

    bintray {
        user = System.env.BINTRAY_USER ?: project["bintray.user"]
        key = System.env.BINTRAY_API_KEY ?: project["bintray.key"]
        publications = ['schemagenGraphQL']
        publish = true
        pkg {
            repo = 'schemagen-graphql'
            name = 'schemagen-graphql'
            desc = 'GraphQL Schema generation in Java'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/bpatters/schemagen-graphql'
        }
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}


dependencies {
    compile 'com.graphql-java:graphql-java:2016-02-05T08-30-28'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.google.guava:guava:19.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile("com.fasterxml.jackson.core:jackson-core:${jacksonVersion}");
    testCompile("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}");
}


